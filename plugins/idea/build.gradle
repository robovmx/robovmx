plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "2.1.20"
    id "org.jetbrains.intellij" version "1.13.2"
}

ext {
    roboVMVersion = '10.2.2.4-SNAPSHOT'
}

group 'com.robovmx'
version "${roboVMVersion}"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://central.sonatype.com/repository/maven-snapshots'}
}

configurations {
    robovm_dist
}

dependencies {
    implementation ("com.robovmx:robovm-dist-compiler:${roboVMVersion}") {
        exclude group: 'com.robovmx', module: 'robovm-rt'
        exclude group: 'com.robovmx', module: 'robovm-objc'
        exclude group: 'com.robovmx', module: 'robovm-bro-bridge'
        exclude group: 'com.robovmx', module: 'robovm-cocoatouch'
        exclude group: 'com.robovmx', module: 'robovm-cacerts-full'
    }
    implementation ("com.robovmx:robovm-ibxcode:${roboVMVersion}") {
        exclude group: 'com.robovmx', module: 'robovm-compiler'
    }
    implementation ("com.robovmx:robovm-templater:${roboVMVersion}") {
        exclude group: 'com.robovmx', module: 'robovm-templates-ios-single-view-no-ib'
        exclude group: 'com.robovmx', module: 'robovm-templates-ios-framework'
        exclude group: 'com.robovmx', module: 'robovm-templates-console'
    }
    robovm_dist "com.robovmx:robovm-dist:${roboVMVersion}:nocompiler@tar.gz"
    implementation 'org.apache.commons:commons-compress:1.25.0'
}

kotlin {
  jvmToolchain(17)
}

intellij {
    version = '2024.2'   // we are compiles against this API version
    plugins = ['java', 'maven', 'gradle']
    updateSinceUntilBuild = false
}

def copyRoboVmDist = tasks.register("copyRoboVmDist", Copy) {
    from configurations.robovm_dist
    into "src/main/resources/"
    rename "robovm-dist-${roboVMVersion}-nocompiler.tar.gz", "robovm-dist"
}


sourceCompatibility = 17
targetCompatibility = 17
tasks.named("patchPluginXml").configure {
    dependsOn copyRoboVmDist
}
